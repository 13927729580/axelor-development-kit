apply from: "${rootDir}/version.gradle"

buildscript {
	apply from: "${rootDir}/gradle/repo.gradle"
	dependencies {
		classpath "org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1"
	}
}

allprojects {
	apply from: "${rootDir}/gradle/repo.gradle"
	apply from: "${rootDir}/gradle/libs.gradle"
}

subprojects {
	apply from: "${rootDir}/gradle/common.gradle"

	if (project.name != 'axelor-shell') {
		apply from: "${rootDir}/gradle/publish.gradle"
	}

	apply plugin: com.axelor.gradle.support.JavaSupport
	apply plugin: com.axelor.gradle.support.EclipseSupport
	apply plugin: com.axelor.gradle.support.LicenseSupport

	group = "com.axelor"
	version = rootProject.version

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	afterEvaluate {
		test {
			forkEvery 1
			maxParallelForks 4

			beforeTest { descriptor ->
				logger.lifecycle("Running: " + descriptor)
			}
		}
	}
}

task updateVersion(type: com.axelor.gradle.tasks.UpdateVersion) {
	description "Update version text in source files."
	group "Axelor"
	processFiles = fileTree(projectDir) {
		include '**/resources/**/*.xml'
		include '**/resources/domain-models.xsd'
		include '**/resources/object-views.xsd'
		include '**/resources/data-import.xsd'
		include '**/resources/templates/**/*.tmpl'
		include '**/data/**/*config.xml'
		include '**/webapp/package.json'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = "3.4.1"
}

apply from: "gradle/javadoc.gradle"
